import org.w3c.dom.Element

String property(String name) {
    return project.getProperty(name)
}

buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.+', changing: true
    }
}

repositories {
    mavenLocal()
    maven {
        name 'LDTTeam - Modding'
        url 'https://ldtteam.jfrog.io/ldtteam/modding/'
    }
    maven {
        url "https://maven.tehnut.info"
    }
    maven {
        url "https://www.cursemaven.com"
    }
    maven {
        url "https://harleyoconnor.com/maven"
    }
    maven {
        url "https://maven.ellpeck.de"
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'maven-publish'

String modName = property("modName")
String modVersion = property("modVersion")
String mcVersion = property("mcVersion")

version = "${mcVersion}-${modVersion}"
group = property("group")
archivesBaseName = modName

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

minecraft {
    mappings channel: 'official', version: mcVersion

    runs {
        client {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'

            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${buildDir}/createSrgToMcp/output.srg"

            if (project.hasProperty('mcUuid')) {
                args '--uuid', property('mcUuid')
            }
            if (project.hasProperty('mcUsername')) {
                args '--username', property('mcUsername')
            }
            if (project.hasProperty('mcAccessToken')) {
                args '--accessToken', property('mcAccessToken')
            }

            mods {
                dtnaturesaura {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'

            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${buildDir}/createSrgToMcp/output.srg"

            mods {
                dtnaturesaura {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'

            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${buildDir}/createSrgToMcp/output.srg"

            args '--mod', 'dtnaturesaura', '--all', '--output', file('src/generated/resources/')

            mods {
                dtnaturesaura {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets.main.resources {
    srcDir 'src/generated/resources'
}

dependencies {
    // Not sure if we need this one, what is a "forge" anyway?
    minecraft "net.minecraftforge:forge:${mcVersion}-${property("forgeVersion")}"

    // Compile Hwyla API, but don't include in runtime.
    compileOnly fg.deobf("mcp.mobius.waila:Hwyla:${property("hwylaVersion")}:api")
    // At runtime, use the full Hwyla mod.
    runtimeOnly fg.deobf("mcp.mobius.waila:Hwyla:${property("hwylaVersion")}")

    // Compile JEI API, but don't include in runtime.
    compileOnly fg.deobf("mezz.jei:jei-${mcVersion}:${property("jeiVersion")}:api")
    // At runtime, use the full JEI mod.
    runtimeOnly fg.deobf("mezz.jei:jei-${mcVersion}:${property("jeiVersion")}")

    // Compile Dynamic Trees, of course.
    implementation fg.deobf("com.ferreusveritas.dynamictrees:DynamicTrees-${mcVersion}:${property("dynamicTreesVersion")}")

    implementation fg.deobf("curse.maven:NaturesAura-306626:3255666")

    // Natures Aura requires Patchouli.
    runtimeOnly fg.deobf("vazkii.patchouli:Patchouli:${property("patchouliVersion")}")

    // At runtime, use suggestion provider fix mod.
    runtimeOnly fg.deobf("com.harleyoconnor.suggestionproviderfix:SuggestionProviderFix:${mcVersion}-${property("suggestionProviderFixVersion")}")
}

jar {
    manifest {
        attributes([
                "Specification-Title": project.name,
                "Specification-Vendor": "Harley O'Connor",
                "Specification-Version": "1",
                "Implementation-Title": project.name,
                "Implementation-Version": project.version,
                "Implementation-Vendor" : "Harley O'Connor",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

task sourcesJar(type: Jar) {
    archiveClassifier.set("sources")
    from sourceSets.main.allJava
}

jar.finalizedBy('reobfJar')

java {
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            setArtifactId("${modName}-${mcVersion}")
            setVersion(modVersion)

            from components.java

            pom {
                url = "https://github.com/Harleyoc1/${modName}"
                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://mit-license.org'
                    }
                }
                scm {
                    connection = "scm:git:git://github.com/Harleyoc1/${modName}.git"
                    developerConnection = "scm:git:ssh://github.com/Harleyoc1/${modName}.git"
                    url = "https://github.com/Harleyoc1/${modName}"
                }
            }

            pom.withXml { xmlProvider ->
                final Element element = xmlProvider.asElement();

                // Clear dependencies.
                element.getChildNodes().each { node ->
                    if (node.getNodeName() == "dependencies")
                        element.removeChild(node)
                }
            }
        }
    }
    repositories {
        maven {
            url "file:///${project.projectDir}/mcmodsrepo"
        }
    }
}

idea {
    module {
        downloadSources = true
    }
}
